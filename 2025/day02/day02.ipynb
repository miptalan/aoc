{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "7f926fe9-3591-438e-abed-8c91430c307c",
   "metadata": {},
   "outputs": [],
   "source": [
    "import os\n",
    "os.environ['PYKX_4_1_ENABLED'] = \"True\"\n",
    "import pykx as kx\n",
    "kx.config\n",
    "kx.util.jupyter_qfirst_enable()"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "b8104feb-32a6-49a3-b9e7-970cda6d0e20",
   "metadata": {},
   "source": [
    "\\--- Day 2: Gift Shop --- [link](https://adventofcode.com/2025/day/2)\n",
    "-------------------------\n",
    "\n",
    "You get inside and take the elevator to its only other stop: the gift shop. \"Thank you for visiting the North Pole!\" gleefully exclaims a nearby sign. You aren't sure who is even allowed to visit the North Pole, but you know you can access the lobby through here, and from there you can access the rest of the North Pole base.\n",
    "\n",
    "As you make your way through the surprisingly extensive selection, one of the clerks recognizes you and asks for your help.\n",
    "\n",
    "As it turns out, one of the younger Elves was playing on a gift shop computer and managed to add a whole bunch of invalid product IDs to their gift shop database! Surely, it would be no trouble for you to identify the invalid product IDs for them, right?\n",
    "\n",
    "They've even checked most of the product ID ranges already; they only have a few product ID ranges (your puzzle input) that you'll need to check. For example:\n",
    "\n",
    "`11-22,95-115,998-1012,1188511880-1188511890,222220-222224,  1698522-1698528,446443-446449,38593856-38593862,565653-565659,  824824821-824824827,2121212118-2121212124`\n",
    "\n",
    "(The ID ranges are wrapped here for legibility; in your input, they appear on a single long line.)\n",
    "\n",
    "The ranges are separated by commas (,); each range gives its first ID and last ID separated by a dash (-).\n",
    "\n",
    "Since the young Elf was just doing silly patterns, you can find the invalid IDs by looking for any ID which is made only of some sequence of digits repeated twice. So, 55 (5 twice), 6464 (64 twice), and 123123 (123 twice) would all be invalid IDs.\n",
    "\n",
    "None of the numbers have leading zeroes; 0101 isn't an ID at all. (101 is a valid ID that you would ignore.)\n",
    "\n",
    "Your job is to find all of the invalid IDs that appear in the given ranges. In the above example:\n",
    "\n",
    "*   11-22 has two invalid IDs, 11 and 22.\n",
    "    \n",
    "*   95-115 has one invalid ID, 99.\n",
    "    \n",
    "*   998-1012 has one invalid ID, 1010.\n",
    "    \n",
    "*   1188511880-1188511890 has one invalid ID, 1188511885.\n",
    "    \n",
    "*   222220-222224 has one invalid ID, 222222.\n",
    "    \n",
    "*   1698522-1698528 contains no invalid IDs.\n",
    "    \n",
    "*   446443-446449 has one invalid ID, 446446.\n",
    "    \n",
    "*   38593856-38593862 has one invalid ID, 38593859.\n",
    "    \n",
    "*   The rest of the ranges contain no invalid IDs.\n",
    "    \n",
    "\n",
    "Adding up all the invalid IDs in this example produces 1227775554.\n",
    "\n",
    "What do you get if you add up all of the invalid IDs?"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 165,
   "id": "4aadac0a-439d-4c2c-b769-31260ba2f485",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "\"11-22,95-115,998-1012,1188511880-1188511890,222220-222224,1698522-1698528,44..\n"
     ]
    }
   ],
   "source": [
    "show line:first read0`:test.txt"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 166,
   "id": "08ac659b-c4b3-473f-80e5-87a84ae67f37",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "\"11-22\"\n",
      "\"95-115\"\n",
      "\"998-1012\"\n",
      "\"1188511880-1188511890\"\n",
      "\"222220-222224\"\n",
      "\"1698522-1698528\"\n",
      "\"446443-446449\"\n",
      "\"38593856-38593862\"\n",
      "\"565653-565659\"\n",
      "\"824824821-824824827\"\n",
      "\"2121212118-2121212124\"\n"
     ]
    }
   ],
   "source": [
    "\",\" vs line"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 167,
   "id": "6f7dd872-d63f-4d70-853f-72fed79261c8",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "11         22        \n",
      "95         115       \n",
      "998        1012      \n",
      "1188511880 1188511890\n",
      "222220     222224    \n",
      "1698522    1698528   \n",
      "446443     446449    \n",
      "38593856   38593862  \n",
      "565653     565659    \n",
      "824824821  824824827 \n",
      "2121212118 2121212124\n"
     ]
    }
   ],
   "source": [
    "show input:{\"J\"$\"-\" vs x} each \",\" vs line"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 168,
   "id": "68b5aa4c-dca3-4960-b3f1-ba21bccab17c",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "11 12 13 14 15 16 17 18 19 20 21 2211 22\n",
      "\n"
     ]
    }
   ],
   "source": [
    "/ generate all numbers in range - use pattern matching form 4.1 for params\n",
    "{[(mn;mx)] mn+til 1+mx-mn} 0N!input 0"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "2d164d23-79c9-422f-905d-3b44987ff947",
   "metadata": {},
   "source": [
    "# Part 1\n",
    "Rely on even-digitness and divisibility of 12341234 by 10001"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 169,
   "id": "10a748f3-795e-4bb2-b874-eebe5fc73759",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "1001 1002 1003 1004 1005 1006 1007 1008 1009 1010 1011 1012\n",
      "998 1012\n"
     ]
    }
   ],
   "source": [
    "/ only even-number-digit numbers are of interest\n",
    "{[(mn;mx)] s where 0=mod[ceiling 10 xlog s:mn+til 1+mx-mn;2]} 0N!input 2"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 170,
   "id": "736311c6-e792-466e-b30e-f53f6afd8bd3",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      ",1010\n"
     ]
    }
   ],
   "source": [
    "/ symmetrical number abab (3rd power of 10) should be divisible by 101: 1+10^(1+3)/2\n",
    "{[(mn;mx)] s where (0=mod[s;1+10 xexp n2 div 2])&0=mod[n2:ceiling 10 xlog s:mn+til 1+mx-mn;2]} input 2"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 171,
   "id": "db99bb31-f1e3-46d7-8c94-9c9a167e7597",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "1227775554\n"
     ]
    }
   ],
   "source": [
    "/ solution\n",
    "sum raze {[(mn;mx)] s where (0=mod[s;1+10 xexp n2 div 2])&0=mod[n2:ceiling 10 xlog s:mn+til 1+mx-mn;2]} each input"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "a5027b42-7496-4f1f-84ea-a48ed300051c",
   "metadata": {},
   "source": [
    "\\--- Part Two ---\n",
    "-----------------\n",
    "\n",
    "The clerk quickly discovers that there are still invalid IDs in the ranges in your list. Maybe the young Elf was doing other silly patterns as well?\n",
    "\n",
    "Now, an ID is invalid if it is made only of some sequence of digits repeated at least twice. So, 12341234 (1234 two times), 123123123 (123 three times), 1212121212 (12 five times), and 1111111 (1 seven times) are all invalid IDs.\n",
    "\n",
    "From the same example as before:\n",
    "\n",
    "*   11-22 still has two invalid IDs, 11 and 22.\n",
    "    \n",
    "*   95-115 now has two invalid IDs, 99 and 111.\n",
    "    \n",
    "*   998-1012 now has two invalid IDs, 999 and 1010.\n",
    "    \n",
    "*   1188511880-1188511890 still has one invalid ID, 1188511885.\n",
    "    \n",
    "*   222220-222224 still has one invalid ID, 222222.\n",
    "    \n",
    "*   1698522-1698528 still contains no invalid IDs.\n",
    "    \n",
    "*   446443-446449 still has one invalid ID, 446446.\n",
    "    \n",
    "*   38593856-38593862 still has one invalid ID, 38593859.\n",
    "    \n",
    "*   565653-565659 now has one invalid ID, 565656.\n",
    "    \n",
    "*   824824821-824824827 now has one invalid ID, 824824824.\n",
    "    \n",
    "*   2121212118-2121212124 now has one invalid ID, 2121212121.\n",
    "    \n",
    "\n",
    "Adding up all the invalid IDs in this example produces 4174379265.\n",
    "\n",
    "What do you get if you add up all of the invalid IDs using these new rules?"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "35323103-e84c-49b5-9329-0a8508ba5c82",
   "metadata": {},
   "source": [
    "2 digits - only 11  -> aa\n",
    "\n",
    "3 digits - only 111 -> aaa\n",
    "\n",
    "4 digits - 101 and 1111 -> abab and aaaa\n",
    "\n",
    "5 digits - only 11111 -> aaaaa\n",
    "\n",
    "6 digits - 1001, 10101 and 111111 -> abcabc, ababab and aaaaaa\n",
    "\n",
    "*in general it is pair-wise factorisations*"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 172,
   "id": "afa3affe-77ef-45b4-aebc-5a39f242774e",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "111111111111\n",
      "10101010101\n",
      "1001001001\n",
      "100010001\n",
      "1000001\n"
     ]
    }
   ],
   "source": [
    "/example for 12\n",
    "111111111111 div 1 / 12 1-digits\n",
    "121212121212 div 12 / 6 2-digits\n",
    "123123123123 div 123 / 4 3-digits\n",
    "123412341234 div 1234 / 3 4-digits\n",
    "123456123456 div 123456 / 2 6-digits"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 173,
   "id": "6424e930-ea66-4c7d-b225-f8da38ab9bcb",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "12 1\n",
      "6  2\n",
      "4  3\n",
      "3  4\n",
      "2  6\n"
     ]
    }
   ],
   "source": [
    "/ simple factorisation\n",
    "ffn:{-1_flip[(x div d;d)]idx:where 0=x mod/:d:1+til x}\n",
    "show factors:ffn 12"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 174,
   "id": "7f266073-2a99-4132-acf9-d2852b00c1a4",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "1001001001\n"
     ]
    }
   ],
   "source": [
    "/ generate \"Nth power of 10 shited m times\" pattern\n",
    "{[d;p] 1+{y+x*y}/[p10;(d-2)#p10:prd p#10]}[4;3]"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 175,
   "id": "5f5eb187-d097-4d44-8efe-e7287b9c3142",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "111111111111 10101010101 1001001001 100010001 1000001\n",
      "1111111111 101010101 100001\n",
      ",111\n",
      ",11\n"
     ]
    }
   ],
   "source": [
    "/ example for 12, 10, 3 and 2\n",
    "{[d;p] 1+{y+x*y}/[p10;(d-2)#p10:prd p#10]}.'ffn 12\n",
    "{[d;p] 1+{y+x*y}/[p10;(d-2)#p10:prd p#10]}.'ffn 10\n",
    "{[d;p] 1+{y+x*y}/[p10;(d-2)#p10:prd p#10]}.'ffn 3\n",
    "{[d;p] 1+{y+x*y}/[p10;(d-2)#p10:prd p#10]}.'ffn 2"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 176,
   "id": "1bad6791-a5e4-47fe-82c4-623ff05db4d6",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "2 | ,11\n",
      "3 | ,111\n",
      "4 | 1111 101\n",
      "5 | ,11111\n",
      "6 | 111111 10101 1001\n",
      "7 | ,1111111\n",
      "8 | 11111111 1010101 10001\n",
      "9 | 111111111 1001001\n",
      "10| 1111111111 101010101 100001\n"
     ]
    }
   ],
   "source": [
    "/ pre-generate divisors\n",
    "k:1_1+til max ceiling 10 xlog input[;1]\n",
    "d:k!{{[d;p] 1+{y+x*y}/[p10;(d-2)#p10:prd p#10]}.'ffn x}each k\n",
    "d"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 177,
   "id": "a734919c-470e-4241-a4f1-9f5919859b4f",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      ",111\n",
      ",111\n",
      ",111\n",
      "1111 101\n",
      "1111 101\n",
      "1111 101\n",
      "1111 101\n",
      "1111 101\n",
      "1111 101\n",
      "1111 101\n",
      "1111 101\n",
      "1111 101\n",
      "1111 101\n",
      "1111 101\n",
      "1111 101\n",
      "998 999 1000 1001 1002 1003 1004 1005 1006 1007 1008 1009 1010 1011 1012\n"
     ]
    }
   ],
   "source": [
    "/ getting divisors \n",
    "{[(mn;mx)] d ceiling 10 xlog s:0N!mn+til 1+mx-mn} input 2"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 178,
   "id": "7ee41cb5-da70-4d10-aff2-06a8055099fc",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "999 1010\n"
     ]
    }
   ],
   "source": [
    "/ any divisor getting 0 remainder?\n",
    "raze {[(mn;mx)] s where any each 0=s mod'd ceiling 10 xlog s:mn+til 1+mx-mn} input 2"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 179,
   "id": "8b255413-48ae-4eb8-99f2-d7ae3730f89b",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "4174379265\n"
     ]
    }
   ],
   "source": [
    "/ solution 2\n",
    "sum raze {[(mn;mx)] s where any each 0=s mod'd ceiling 10 xlog s:mn+til 1+mx-mn} each input"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "24e9e501-7d96-4415-91c4-f80bdf27d9f7",
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Python 3 (ipykernel)",
   "language": "python",
   "name": "python3"
  },
  "language_info": {
   "codemirror_mode": {
    "name": "ipython",
    "version": 3
   },
   "file_extension": ".py",
   "mimetype": "text/x-python",
   "name": "python",
   "nbconvert_exporter": "python",
   "pygments_lexer": "ipython3",
   "version": "3.12.12"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
