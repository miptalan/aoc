{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": 1,
   "id": "3c186c7e-7511-404d-b894-4ac28a8fec17",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "PyKX now running in 'jupyter_qfirst' mode. All cells by default will be run as q code. \n",
      "Include '%%py' at the beginning of each cell to run as python code. \n"
     ]
    }
   ],
   "source": [
    "import os\n",
    "os.environ['PYKX_4_1_ENABLED'] = \"True\"\n",
    "import pykx as kx\n",
    "kx.util.jupyter_qfirst_enable()"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "c8b5561e-18a8-40ce-b57b-ec1ccc174460",
   "metadata": {},
   "source": [
    "## --- Day 4: Printing Department --- [link](https://adventofcode.com/2025/day/4)\n",
    "You ride the escalator down to the printing department. They're clearly getting ready for Christmas; they have lots of large rolls of paper everywhere, and there's even a massive printer in the corner (to handle the really big print jobs).\n",
    "\n",
    "Decorating here will be easy: they can make their own decorations. What you really need is a way to get further into the North Pole base while the elevators are offline.\n",
    "\n",
    "\"Actually, maybe we can help with that,\" one of the Elves replies when you ask for help. \"We're pretty sure there's a cafeteria on the other side of the back wall. If we could break through the wall, you'd be able to keep moving. It's too bad all of our forklifts are so busy moving those big rolls of paper around.\"\n",
    "\n",
    "If you can optimize the work the forklifts are doing, maybe they would have time to spare to break through the wall.\n",
    "\n",
    "The rolls of paper (@) are arranged on a large grid; the Elves even have a helpful diagram (your puzzle input) indicating where everything is located.\n",
    "\n",
    "For example:\n",
    "\n",
    "    ..@@.@@@@.\n",
    "    @@@.@.@.@@\n",
    "    @@@@@.@.@@\n",
    "    @.@@@@..@.\n",
    "    @@.@@@@.@@\n",
    "    .@@@@@@@.@\n",
    "    .@.@.@.@@@\n",
    "    @.@@@.@@@@\n",
    "    .@@@@@@@@.\n",
    "    @.@.@@@.@.\n",
    "\n",
    "The forklifts can only access a roll of paper if there are fewer than four rolls of paper in the eight adjacent positions. If you can figure out which rolls of paper the forklifts can access, they'll spend less time looking and more time breaking down the wall to the cafeteria.\n",
    "\n",
    "In this example, there are 13 rolls of paper that can be accessed by a forklift (marked with x):\n",
    "\n",
    "    ..xx.xx@x.\n",
    "    x@@.@.@.@@\n",
    "    @@@@@.x.@@\n",
    "    @.@@@@..@.\n",
    "    x@.@@@@.@x\n",
    "    .@@@@@@@.@\n",
    "    .@.@.@.@@@\n",
    "    x.@@@.@@@@\n",
    "    .@@@@@@@@.\n",
    "    x.x.@@@.x.\n",
    "\n",
    "Consider your complete diagram of the paper roll locations. How many rolls of paper can be accessed by a forklift?"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 3,
   "id": "215df96a-a1f4-43bb-a7af-14d5f9d66298",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "\"..@@.@@@@.\"\n",
      "\"@@@.@.@.@@\"\n",
      "\"@@@@@.@.@@\"\n",
      "\"@.@@@@..@.\"\n",
      "\"@@.@@@@.@@\"\n",
      "\".@@@@@@@.@\"\n",
      "\".@.@.@.@@@\"\n",
      "\"@.@@@.@@@@\"\n",
      "\".@@@@@@@@.\"\n",
      "\"@.@.@@@.@.\"\n"
     ]
    }
   ],
   "source": [
    "show input:read0`:test.txt"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 5,
   "id": "0af7b609-745f-488b-b958-1a63b1ac2a84",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "10 10\n"
     ]
    }
   ],
   "source": [
    "/ prepare grid sizes\n",
    "show (ncol;nrow):count each input (0;::)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 6,
   "id": "2e4960ee-a8df-4bff-9805-7ae9a7cfd0b0",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "..@@.@@@@.@@@.@.@.@@@@@@@.@.@@@.@@@@..@.@@.@@@@.@@.@@@@@@@.@.@.@.@.@@@@.@@@.@@@@.@@@@@@@@.@.@.@@@.@.\n"
     ]
    }
   ],
   "source": [
    "/ flatten\n",
    "input ./:idx:cross[til nrow;til ncol]"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 7,
   "id": "ba4a41c6-70f7-4653-bc4c-f70a4b1c37a1",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "0 2\n",
      "0 3\n",
      "0 5\n",
      "0 6\n",
      "0 7\n",
      "0 8\n",
      "1 0\n",
      "1 1\n",
      "1 2\n",
      "1 4\n",
      "1 6\n",
      "1 8\n",
      "1 9\n",
      "2 0\n",
      "2 1\n",
      "2 2\n",
      "2 3\n",
      "2 4\n",
      "2 6\n",
      "2 8\n",
      "2 9\n",
      "3 0\n",
      "..\n"
     ]
    }
   ],
   "source": [
    "/ determine coordinates of rolls\n",
    "show idx:idx where \"@\"=input ./:idx:cross[til nrow;til ncol]"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 8,
   "id": "69833c45-53c5-433f-a14e-f7be6825647b",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "..@@@@@@\n"
     ]
    }
   ],
   "source": [
    "/ get neighbours\n",
    "{[grid;coord] grid ./:coord+/:(-1 -1;-1 0;-1 1;0 -1;0 1;1 -1;1 0;1 1)}[input;1 1]"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 10,
   "id": "6113d841-8f10-4d4a-962b-d1f74f8b380e",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "3 3 3 3 4 3 3 6 6 4 4 5 4 4 7 6 7 5 2 4 4 4 6 7 7 6 4 3 5 7 8 7 5 4 3 4 6 5 7..\n"
     ]
    }
   ],
   "source": [
    "/ count roll neighbours for all\n",
    "fn:{[grid;coord] sum \"@\"=grid ./:coord+/:(-1 -1;-1 0;-1 1;0 -1;0 1;1 -1;1 0;1 1)}\n",
    "fn[input] each idx"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 11,
   "id": "04123744-b2ca-40b4-8774-a969a377ff11",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "13i\n"
     ]
    }
   ],
   "source": [
    "/ how many got less than 4?\n",
    "sum 4>fn[input] each idx"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 19,
   "id": "c757a654-16c5-447b-9d10-610fc0467536",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "13i\n"
     ]
    }
   ],
   "source": [
    "/ full part1 with globals\n",
    "input:read0`:test.txt\n",
    "NB:(-1 -1;-1 0;-1 1;0 -1;0 1;1 -1;1 0;1 1)\n",
    "sum 4>{sum \"@\"=input ./:x+/:NB} each idx where \"@\"=input ./:idx:.[cross;'[til;count]'[input (0;::)]]"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "822805a3-00c6-4174-946e-0534b61d8be0",
   "metadata": {},
   "source": [
    "## --- Part Two ---\n",
    "Now, the Elves just need help accessing as much of the paper as they can.\n",
    "\n",
    "Once a roll of paper can be accessed by a forklift, it can be removed. Once a roll of paper is removed, the forklifts might be able to access more rolls of paper, which they might also be able to remove. How many total rolls of paper could the Elves remove if they keep repeating this process?\n",
    "\n",
    "Starting with the same example as above, here is one way you could remove as many rolls of paper as possible, using highlighted @ to indicate that a roll of paper is about to be removed, and using x to indicate that a roll of paper was just removed:\n",
    "\n",
    "    Initial state:\n",
    "    ..@@.@@@@.\n",
    "    @@@.@.@.@@\n",
    "    @@@@@.@.@@\n",
    "    @.@@@@..@.\n",
    "    @@.@@@@.@@\n",
    "    .@@@@@@@.@\n",
    "    .@.@.@.@@@\n",
    "    @.@@@.@@@@\n",
    "    .@@@@@@@@.\n",
    "    @.@.@@@.@.\n",
    "    \n",
    "    Remove 13 rolls of paper:\n",
    "    ..xx.xx@x.\n",
    "    x@@.@.@.@@\n",
    "    @@@@@.x.@@\n",
    "    @.@@@@..@.\n",
    "    x@.@@@@.@x\n",
    "    .@@@@@@@.@\n",
    "    .@.@.@.@@@\n",
    "    x.@@@.@@@@\n",
    "    .@@@@@@@@.\n",
    "    x.x.@@@.x.\n",
    "    \n",
    "    Remove 12 rolls of paper:\n",
    "    .......x..\n",
    "    .@@.x.x.@x\n",
    "    x@@@@...@@\n",
    "    x.@@@@..x.\n",
    "    .@.@@@@.x.\n",
    "    .x@@@@@@.x\n",
    "    .x.@.@.@@@\n",
    "    ..@@@.@@@@\n",
    "    .x@@@@@@@.\n",
    "    ....@@@...\n",
    "    \n",
    "    Remove 7 rolls of paper:\n",
    "    ..........\n",
    "    .x@.....x.\n",
    "    .@@@@...xx\n",
    "    ..@@@@....\n",
    "    .x.@@@@...\n",
    "    ..@@@@@@..\n",
    "    ...@.@.@@x\n",
    "    ..@@@.@@@@\n",
    "    ..x@@@@@@.\n",
    "    ....@@@...\n",
    "    \n",
    "    Remove 5 rolls of paper:\n",
    "    ..........\n",
    "    ..x.......\n",
    "    .x@@@.....\n",
    "    ..@@@@....\n",
    "    ...@@@@...\n",
    "    ..x@@@@@..\n",
    "    ...@.@.@@.\n",
    "    ..x@@.@@@x\n",
    "    ...@@@@@@.\n",
    "    ....@@@...\n",
    "    \n",
    "    Remove 2 rolls of paper:\n",
    "    ..........\n",
    "    ..........\n",
    "    ..x@@.....\n",
    "    ..@@@@....\n",
    "    ...@@@@...\n",
    "    ...@@@@@..\n",
    "    ...@.@.@@.\n",
    "    ...@@.@@@.\n",
    "    ...@@@@@x.\n",
    "    ....@@@...\n",
    "    \n",
    "    Remove 1 roll of paper:\n",
    "    ..........\n",
    "    ..........\n",
    "    ...@@.....\n",
    "    ..x@@@....\n",
    "    ...@@@@...\n",
    "    ...@@@@@..\n",
    "    ...@.@.@@.\n",
    "    ...@@.@@@.\n",
    "    ...@@@@@..\n",
    "    ....@@@...\n",
    "    \n",
    "    Remove 1 roll of paper:\n",
    "    ..........\n",
    "    ..........\n",
    "    ...x@.....\n",
    "    ...@@@....\n",
    "    ...@@@@...\n",
    "    ...@@@@@..\n",
    "    ...@.@.@@.\n",
    "    ...@@.@@@.\n",
    "    ...@@@@@..\n",
    "    ....@@@...\n",
    "    \n",
    "    Remove 1 roll of paper:\n",
    "    ..........\n",
    "    ..........\n",
    "    ....x.....\n",
    "    ...@@@....\n",
    "    ...@@@@...\n",
    "    ...@@@@@..\n",
    "    ...@.@.@@.\n",
    "    ...@@.@@@.\n",
    "    ...@@@@@..\n",
    "    ....@@@...\n",
    "    \n",
    "    Remove 1 roll of paper:\n",
    "    ..........\n",
    "    ..........\n",
    "    ..........\n",
    "    ...x@@....\n",
    "    ...@@@@...\n",
    "    ...@@@@@..\n",
    "    ...@.@.@@.\n",
    "    ...@@.@@@.\n",
    "    ...@@@@@..\n",
    "    ....@@@...\n",
    "\n",
    "Stop once no more rolls of paper are accessible by a forklift. In this example, a total of 43 rolls of paper can be removed.\n",
    "\n",
    "Start with your original diagram. How many rolls of paper in total can be removed by the Elves and their forklifts?"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 24,
   "id": "17a52916-a4e6-4c92-8d33-0ee88ed076e5",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "43\n"
     ]
    }
   ],
   "source": [
    "/ find indicies to remove, amend-at, repeat until no more to remove\n",
    "fn2:{[(grid;removed)]\n",
    "    grid:./[grid;removed;:;\".\"];\n",
    "    idx:idx where \"@\"=grid ./:idx:.[cross;'[til;count]'[grid(0;::)]];\n",
    "    (grid;idx where 4>{sum \"@\"=x ./:y+/:NB}[grid] each idx)\n",
    " };\n",
    "res:(fn2\\)(input;())\n",
    "count raze res[;1]"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "4fb76a57-2f60-4c11-b53d-d85c1d71199d",
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Python 3 (ipykernel)",
   "language": "python",
   "name": "python3"
  },
  "language_info": {
   "codemirror_mode": {
    "name": "ipython",
    "version": 3
   },
   "file_extension": ".py",
   "mimetype": "text/x-python",
   "name": "python",
   "nbconvert_exporter": "python",
   "pygments_lexer": "ipython3",
   "version": "3.12.12"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
