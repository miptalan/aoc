{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": 3,
   "id": "88642dd9-5e69-43e2-b332-ee00c82a0235",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "PyKX now running in 'jupyter_qfirst' mode. All cells by default will be run as q code. \n",
      "Include '%%py' at the beginning of each cell to run as python code. \n"
     ]
    }
   ],
   "source": [
    "import os\n",
    "os.environ['PYKX_4_1_ENABLED'] = \"True\"\n",
    "import pykx as kx\n",
    "kx.util.jupyter_qfirst_enable()"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "3dc4a7ca-7193-4907-b4b6-64015233653d",
   "metadata": {},
   "source": [
    "## --- Day 7: Laboratories --- [link](https://adventofcode.com/2025/day/7)\n",
    "You thank the cephalopods for the help and exit the trash compactor, finding yourself in the familiar halls of a North Pole research wing.\n",
    "\n",
    "Based on the large sign that says \"teleporter hub\", they seem to be researching teleportation; you can't help but try it for yourself and step onto the large yellow teleporter pad.\n",
    "\n",
    "Suddenly, you find yourself in an unfamiliar room! The room has no doors; the only way out is the teleporter. Unfortunately, the teleporter seems to be leaking magic smoke.\n",
    "\n",
    "Since this is a teleporter lab, there are lots of spare parts, manuals, and diagnostic equipment lying around. After connecting one of the diagnostic tools, it helpfully displays error code 0H-N0, which apparently means that there's an issue with one of the tachyon manifolds.\n",
    "\n",
    "You quickly locate a diagram of the tachyon manifold (your puzzle input). A tachyon beam enters the manifold at the location marked S; tachyon beams always move downward. Tachyon beams pass freely through empty space (.). However, if a tachyon beam encounters a splitter (^), the beam is stopped; instead, a new tachyon beam continues from the immediate left and from the immediate right of the splitter."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 41,
   "id": "266f3401-0892-4e86-8b91-1e28a98fcd20",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "\".......S.......\"\n",
      "\"...............\"\n",
      "\".......^.......\"\n",
      "\"...............\"\n",
      "\"......^.^......\"\n",
      "\"...............\"\n",
      "\".....^.^.^.....\"\n",
      "\"...............\"\n",
      "\"....^.^...^....\"\n",
      "\"...............\"\n",
      "\"...^.^...^.^...\"\n",
      "\"...............\"\n",
      "\"..^...^.....^..\"\n",
      "\"...............\"\n",
      "\".^.^.^.^.^...^.\"\n",
      "\"...............\"\n"
     ]
    }
   ],
   "source": [
    "show input:read0`:test.txt"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 42,
   "id": "54f9c6f1-6e01-4ba7-b490-6948678be63e",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "15\n",
      ",7\n"
     ]
    }
   ],
   "source": [
    "show N:count input 0\n",
    "show S:where \"S\"=input 0\n",
    "delim:\"^\""
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 43,
   "id": "587d8b74-52cb-4d55-b76a-ffb44ef5d1b2",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "21\"(,7;\\\"...............\\\")\"\n",
      "\n",
      "\"(`s#,7;\\\".......^.......\\\")\"\n",
      "\"(`s#6 8;\\\"...............\\\")\"\n",
      "\"(`s#6 8;\\\"......^.^......\\\")\"\n",
      "\"(`s#5 7 9;\\\"...............\\\")\"\n",
      "\"(`s#5 7 9;\\\".....^.^.^.....\\\")\"\n",
      "\"(`s#4 6 8 10;\\\"...............\\\")\"\n",
      "\"(`s#4 6 8 10;\\\"....^.^...^....\\\")\"\n",
      "\"(`s#3 5 7 8 9 11;\\\"...............\\\")\"\n",
      "\"(`s#3 5 7 8 9 11;\\\"...^.^...^.^...\\\")\"\n",
      "\"(`s#2 4 6 7 8 10 12;\\\"...............\\\")\"\n",
      "\"(`s#2 4 6 7 8 10 12;\\\"..^...^.....^..\\\")\"\n",
      "\"(`s#1 3 4 5 7 8 10 11 13;\\\"...............\\\")\"\n",
      "\"(`s#1 3 4 5 7 8 10 11 13;\\\".^.^.^.^.^...^.\\\")\"\n",
      "\"(`s#0 2 4 6 8 10 11 12 14;\\\"...............\\\")\"\n"
     ]
    }
   ],
   "source": [
    "/ process line by line with accumulator\n",
    "result:{[(beams;cnt);line]\n",
    "    show .Q.s1(beams;line);\n",
    "    cnt+:count splitIdx:beams inter where delim=line; / where we hit splitter\n",
    "    beams,:newIdx where (newIdx:raze -1 1+/:splitIdx) within (0;N-1); / add neighbours controlling for boundaries    \n",
    "    (asc distinct beams except splitIdx;cnt) / remove beams that hit splitters\n",
    " }/[(S;0);1_input]\n",
    "\n",
    "/ part 1 - counter\n",
    "result 1"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 46,
   "id": "c4cbde02-0d1b-4399-9d59-35159dcf048b",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "40\n"
     ]
    }
   ],
   "source": [
    "/ for part 2 the number of paths is too large to generate individual beams, count via dict\n",
    "/ process line by line with accumulator\n",
    "result:{[beamsDict;line]\n",
    "    beams:key beamsDict;\n",
    "    splitIdx:beams inter where delim=line;\n",
    "    if[0=count splitIdx;:beamsDict]; / early return\n",
    "    currCnt:beamsDict splitIdx; \n",
    "    newBeamsDict:sum each raze[2#/:currCnt] group raze -1 1+/:splitIdx; / count beams in each position\n",
    "    beams:distinct beams,key newBeamsDict;\n",
    "    (beams except splitIdx)#beamsDict+newBeamsDict\n",
    " }/[S!enlist 1;1_input]\n",
    "\n",
    "/ part 2 - sum number of beams at the end\n",
    "sum value result"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "69cd7cc2-6453-49cb-94cc-47e71b6b22ce",
   "metadata": {},
   "outputs": [],
   "source": [
    "|"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Python 3 (ipykernel)",
   "language": "python",
   "name": "python3"
  },
  "language_info": {
   "codemirror_mode": {
    "name": "ipython",
    "version": 3
   },
   "file_extension": ".py",
   "mimetype": "text/x-python",
   "name": "python",
   "nbconvert_exporter": "python",
   "pygments_lexer": "ipython3",
   "version": "3.12.12"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
