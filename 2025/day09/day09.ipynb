{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": 1,
   "id": "133868bf-eea6-409c-b04d-e4818069c223",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "PyKX now running in 'jupyter_qfirst' mode. All cells by default will be run as q code. \n",
      "Include '%%py' at the beginning of each cell to run as python code. \n"
     ]
    }
   ],
   "source": [
    "import os\n",
    "os.environ['PYKX_4_1_ENABLED'] = \"True\"\n",
    "import pykx as kx\n",
    "kx.util.jupyter_qfirst_enable()"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "014d49a7-00bf-44d5-b4a3-c1a032e1732d",
   "metadata": {},
   "source": [
    "## --- Day 9: Movie Theater --- [link](https://adventofcode.com/2025/day/9)\n",
    "\n",
    "You slide down the firepole in the corner of the playground and land in the North Pole base movie theater!\n",
    "\n",
    "The movie theater has a big tile floor with an interesting pattern. Elves here are redecorating the theater by switching out some of the square tiles in the big grid they form. Some of the tiles are red; the Elves would like to find the largest rectangle that uses red tiles for two of its opposite corners. They even have a list of where the red tiles are located in the grid (your puzzle input)."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 86,
   "id": "b86e6104-5aab-49c9-825e-1f68dcf483a1",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "7  1\n",
      "11 1\n",
      "11 7\n",
      "9  7\n",
      "9  5\n",
      "2  5\n",
      "2  3\n",
      "7  3\n"
     ]
    }
   ],
   "source": [
    "show reds:get each read0`:test.txt"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 87,
   "id": "3a4e03a0-5d98-48d6-89cb-91936d059d21",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "50\n"
     ]
    }
   ],
   "source": [
    "/part 1\n",
    "area:{[(p1;p2)] prd 1+abs .[-]reds(p1;p2)}\n",
    "/ max area over all non-repeating pairs\n",
    "max area@'pairs:{l where (<).'l:x cross x} til count reds"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 88,
   "id": "f339ad20-a562-4c46-b684-a6d9ee6b7bb1",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "7  1 11 1\n",
      "11 1 11 7\n",
      "11 7 9  7\n",
      "9 7  9 5 \n",
      "9 5  2 5 \n",
      "2 5  2 3 \n",
      "2 3  7 3 \n",
      "7 3  7 1 \n",
      "\"-\"\n",
      "11 1 11 7\n",
      "9 7  9 5 \n",
      "2 5  2 3 \n",
      "7 3  7 1 \n",
      "\"-\"\n",
      "7  1 11 1\n",
      "11 7 9  7\n",
      "9 5  2 5 \n",
      "2 3  7 3 \n"
     ]
    }
   ],
   "source": [
    "/ part 2\n",
    "/ tricky polygon shape, good thing - orthogonal\n",
    "/ so each candidate rectangle needs to be checked for\n",
    "/ 1) insides of rectangle being inside polygon - use raytracing\n",
    "/ 2) polygon edges not intersecting rectangle edges\n",
    "\n",
    "show edges:{2#x rotate reds} each til count reds / edges of polygon\n",
    "show \"-\"\n",
    "show vedges:edges where (=).'edges[;0 1;0] / vertical edges\n",
    "show \"-\"\n",
    "show hedges:edges where (<>).'edges[;0 1;0] / vertical edges"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 89,
   "id": "9fb0b468-a994-4398-862d-23605abf71a0",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "2i\n",
      "12\n"
     ]
    }
   ],
   "source": [
    "/ raytrace - even number of edge crossings - outside, odd - inside\n",
    "show xmax:1+max reds[;0]\n",
    "\n",
    "ray:{[(start;stop)] start+til 1+`long$stop-start}\n",
    "\n",
    "sum any 1_'differ each ray[(1.5;xmax+0.5)]</:1 2 3"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 90,
   "id": "654695af-4e3c-4ffc-9a8b-958777fb4264",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "1b\n",
      "(9 7;9 5)\n"
     ]
    }
   ],
   "source": [
    "/ edge intersection check\n",
    "edgeInter:{[(r1;r2);(p1;p2)] \n",
    "    / 0b - no intersection\n",
    "    (minpx;minpy):min(p1;p2);\n",
    "    (maxpx;maxpy):max(p1;p2);\n",
    "    (minrx;minry):min(r1;r2);\n",
    "    (maxrx;maxry):max(r1;r2);\n",
    "    if[minpx=maxpx; / vertical edge\n",
    "        :$[(minpx<=minrx)|minpx>=maxrx; / not within rectangle x-boundaries\n",
    "            0b;\n",
    "            max[(minpy;minry)]<min(maxpy;maxry) / otherwise - check y overlap\n",
    "            ];\n",
    "     ];\n",
    "    if[minpy=maxpy; / horizontal edge\n",
    "        :$[(minpy<=minry)|minpy>=maxry; / not within rectangle y-boundaries\n",
    "            0b;\n",
    "            max[(minpx;minrx)]<min(maxpx;maxrx) / otherwise - check x overlap\n",
    "            ];\n",
    "     ];\n",
    " };\n",
    "edgeInter[(2 3;11 7);0N!vedges 1]"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 91,
   "id": "5295ade1-224d-470b-be44-a4a62e75fbca",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "00000000b\n"
     ]
    }
   ],
   "source": [
    "edgeInter[(2 3;9 5)] each edges"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 92,
   "id": "fbfd1d15-4c40-45a0-80da-6e1975ee6155",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "0 1\n",
      "0 4\n",
      "0 6\n",
      "0 7\n",
      "1 3\n",
      "1 4\n",
      "1 7\n",
      "2 4\n",
      "3 4\n",
      "3 5\n",
      "4 5\n",
      "4 6\n",
      "4 7\n",
      "5 6\n",
      "5 7\n",
      "6 7\n"
     ]
    }
   ],
   "source": [
    "show insidePairs:pairs where {[(i1;i2)] / indicies in red tiles list\n",
    "    / rect diag coords, top left corner and interior point\n",
    "    (ipx;ipy):0.5+min(r1;r2):reds(i1;i2);\n",
    "    / horizontal ray from internal point\n",
    "    ihr:ray(ipx;0.5+xmax);\n",
    "    / vertical edges on the same y-level as internal point\n",
    "    vex:vedges[;0;0] where ipy within/:vedges[;0 1;1];\n",
    "    / if interior point not inside - exit early\n",
    "    if[0=(sum any 1_'differ each ihr</:vex) mod 2;:0b];\n",
    "    / check all edges strictly outside rectangle boundaries\n",
    "    not any edgeInter[(r1;r2)] each edges\n",
    " } each pairs"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 93,
   "id": "8f29b26a-8749-4d6b-b012-5b4a3168528c",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "24\n"
     ]
    }
   ],
   "source": [
    "max area@'insidePairs"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "50e34798-b340-48c7-b36f-28473d8b010f",
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Python 3 (ipykernel)",
   "language": "python",
   "name": "python3"
  },
  "language_info": {
   "codemirror_mode": {
    "name": "ipython",
    "version": 3
   },
   "file_extension": ".py",
   "mimetype": "text/x-python",
   "name": "python",
   "nbconvert_exporter": "python",
   "pygments_lexer": "ipython3",
   "version": "3.12.12"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
